/datum/round_event_control/secretsanta
	name = "Secret Santa"
	holidayID = FESTIVE_SEASON
	typepath = /datum/round_event/secretsanta
	weight = -1 //forces it to be called, regardless of weight
	max_occurrences = 1
	earliest_start = 5 MINUTES
	category = EVENT_CATEGORY_HOLIDAY
	description = "Gives everyone a secret santa to gift something to!"
	players_amt = 3

/datum/round_event/secretsanta/start()
	var/list/mob/living/candidates = list()
	for(var/mob/living/candidate as anything in GLOB.mob_living_list)
		if(is_candidate(candidate))
			candidates += candidate

	if(length(candidates) < 2)
		return // shouldn't happen due to players_amt, but just in case

	var/mob/living/first_gifter = pick_n_take(candidates)
	var/mob/living/last_gifter = first_gifter

	while(length(candidates))
		var/mob/living/next_gifter = pick_n_take(candidates)
		assign_secret_santa(next_gifter, last_gifter)
		last_gifter = next_gifter

	assign_secret_santa(first_gifter, last_gifter)

	// melbert todo : latejoiner handling

/datum/round_event/secretsanta/proc/is_candidate(mob/living/some_mob)
	if(isnull(some_mob.mind))
		return FALSE
	var/turf/some_turf = get_turf(some_mob)
	// admin characters need not apply, you can spawn whatever you want for christmas
	if(is_centcom_level(some_turf.z))
		return FALSE
	if(some_mob.mind.assigned_role.job_flags & JOB_CREW_MEMBER)
		return TRUE
	// sure, we can include silicons in on this. just don't grab ones in deep space i guess
	if(issilicon(some_mob) && (is_station_level(some_turf.z) || is_mining_level(some_turf.z)))
		return TRUE
	return FALSE

/datum/round_event/secretsanta/proc/assign_secret_santa(mob/living/gifter, mob/living/recipient)
	var/datum/antagonist/secret_santa/secret = gifter.mind.add_antagonist(__IMPLIED_TYPE__)
	secret.recipient = recipient.mind
	to_chat(gifter, examine_block(span_green("[span_big("You are <b>[secret.recipient.name]</b>'s Secret Santa!")]<br><br>\
		Come up with an appropriate gift and wait for the festivities to begin!<br>\
		(If you need help coming up with an idea, try asking their co-workers and friends, or even <i>pray</i> for inspiration!)")))

/datum/round_event/secretsanta/announce(fake)
	if(fake)
		return

	priority_announce("Ho ho ho! The annual Nanotrasenâ„¢ Secret Santa is starting! Everyone has been assigned a Secret Santa of another crew member.<br><br>\
		When and where you decide to exchange gifts is up to you all to decide - just remember to keep your assignment a secret!<br>Happy holidays!")

/datum/antagonist/secret_santa
	name = "\improper Secret Santa"
	roundend_category = "secret santa"
	show_in_antagpanel = FALSE
	prevent_roundtype_conversion = FALSE
	ui_name = null
	silent = TRUE
	/// Keep track who we're giving a gift to
	var/datum/mind/recipient

/datum/antagonist/secret_santa/roundend_report()
	return "&bull; [owner.name] had [recipient.name] as [owner.p_their()] Secret Santa recipient."
